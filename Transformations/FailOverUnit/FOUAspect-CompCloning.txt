aspect FOU()
{
  assembly_clone();
}

strategy assembly_clone()
{
  declare container, src, dest : model;
  declare f : folder;
  
  f := rootFolder().findFolder("ComponentImplementations");
  if (f.isNull()) then
    show("Can't find folder = ComponentImplementations");
  else
    container := f.findModel("GPSImpl");
	if (container.isNull()) then
	  show("Can't find container = GPSImpl");
    else
      src := container.findModel("GPSAssembly");
      if (src.isNull()) then
        show("Can't find assembly = GPSAssembly");
      else
        dest := src.clone(container);
        dest.setName(src.name() + "_Replica");
        fou_list := dest.models("FailOverUnit");
        fou_list->ProcessFOU(dest);
        fou_list->RemoveFOU(dest);
      endif;
	endif;
  endif;
}

strategy ProcessFOU(parent_assembly : model)
{
  declare r : integer;
  declare fou : model;

  fou := self;
  r := fou.getAttribute("Replica");

  parent_assembly.connections("ComponentQoS")
                 ->select (c | c.destination() == fou)
                 ->createReplicas(parent_assembly, r, 1);
}

strategy createReplicas(parent_assembly : model; replica, loop : integer)
{
  declare connection : object;

  connection := self;

  if (loop<=replica) then
     connection.source().cloneComponent(parent_assembly, loop);
     connection.createReplicas(parent_assembly, replica, loop+1);
  endif;
}

strategy cloneComponent(parent_assembly : model; count : integer)
{
  declare comp_src, comp_dst : model;

  comp_src := self;

  comp_dst := comp_src.clone(parent_assembly);
  comp_dst.setName(comp_src.name() + "_Replica" + intToString(count));
}

strategy RemoveFOU(parent_assembly : model)
{
  parent_assembly.removeModel(self);
}
