// -*- IDL -*-

//=============================================================================
/**
 * @file    CUTS.idl
 *
 * $Id$
 *
 * This file contains the information shared between all components in
 * Component Workload Emulator (CoWorkEr) Utilization Test Suite (CUTS).
 *
 * @author James H. Hill <hillj@isis.vanderbilt.edu>
 */
//=============================================================================

#ifndef _CUTS_IDL_
#define _CUTS_IDL_

#include "cuts/performance.idl"
#include <Components.idl>

module CUTS
{
  /**
   * Dummy event with no payload. This event can serve as a placeholder
   * during emulation until more is learned about the event types.
   */
  eventtype Empty_Event
  {
    /// Id of the event's sender.
    public long sender;
  };

  /**
   * Dummy event with a payload. It allows experiments to transmit events
   * with dummy data associated with it. This event can serve as a placeholder
   * during emulation until more is learned about the event types.
   */
  eventtype Payload_Event : Empty_Event
  {
    /// Size of the events payload.
    public long size;

    /// The payload for the event.
    public string payload;
  };

  exception Registration_Failed   { };
  exception Registration_Limit    { };

  //===========================================================================
  /**
   * @interface Benchmark_Agent
   *
   * Collection object that all components contain internally. This
   * allows an external object to poll each component during runtime
   * and collect performance metrics.
   */
  //===========================================================================

  struct Port_Description
  {
    unsigned long uid;

    string name;
  };

  typedef sequence <Port_Description> Port_Descriptions;

  struct Component_Description
  {
    string type;

    Port_Descriptions sinks;

    Port_Descriptions sources;
  };

  struct Host_Description
  {
    string ipaddr;

    string hostname;
  };

  //===========================================================================
  /**
   * @interface Benchmark_Agent
   */
  //===========================================================================

  interface Benchmark_Agent
  {
    /// Get the performance data from the agent.
    void collect_performance_data (out Component_Metric data);
  };

  //===========================================================================
  /**
   * @struct Component_Registration
   *
   * Registration information about the component. This allows a component
   * to let a monitoring object/component realize the existence of active
   * objects in the system. The registration information contains the details
   * of the component, such as type, instance name, and more importantly the
   * Benchmark_Agent for the component (if applicable) so that the monitoring
   * component can poll it periodically for performance information.
   */
  //===========================================================================

  struct Component_Registration
  {
    /// Name of the component.
    string name;

    /// Description of the component's type.
    Component_Description component_info;

    /// Description of the component's host.
    Host_Description host_info;

    /// Benchmark agent of the component.
    Benchmark_Agent agent;
  };

  //===========================================================================
  /**
   * @interface Testing_Service
   */
  //===========================================================================

  interface Testing_Service
  {
    long register_component (in Component_Registration cr)
      raises (Registration_Failed,
              Registration_Limit);

    void unregister_component (in Component_Registration cr);
  };
};

#endif  // !defined _CUTS_IDL_
